//================= Hercules Script =======================================
//=       _   _                     _
//=      | | | |                   | |
//=      | |_| | ___ _ __ ___ _   _| | ___  ___
//=      |  _  |/ _ \ '__/ __| | | | |/ _ \/ __|
//=      | | | |  __/ | | (__| |_| | |  __/\__ \
//=      \_| |_/\___|_|  \___|\__,_|_|\___||___/
//================= License ===============================================
//= This file is part of Hercules.
//= http://herc.ws - http://github.com/HerculesWS/Hercules
//=
//= Copyright (C) 2012-2015  Hercules Dev Team
//= Copyright (C)  rAthena Dev Team
//= Copyright (C)  Euphy
//= Copyright (C)  Daegaladh
//= Copyright (C)  eAthena Dev Team
//=
//= Hercules is free software: you can redistribute it and/or modify
//= it under the terms of the GNU General Public License as published by
//= the Free Software Foundation, either version 3 of the License, or
//= (at your option) any later version.
//=
//= This program is distributed in the hope that it will be useful,
//= but WITHOUT ANY WARRANTY; without even the implied warranty of
//= MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//= GNU General Public License for more details.
//=
//= You should have received a copy of the GNU General Public License
//= along with this program.  If not, see <http://www.gnu.org/licenses/>.
//=========================================================================
//= Renters (Renewal)
//================= Description ===========================================
//= Renewal-specific breeder NPCs.
//================= Current Version =======================================
//= 2.1
//=========================================================================

//== Dragon Breeder ========================================
job3_rune01,88,62,5	script	Dragon Breeder	8W_SOLDIER,{
	mes "[Dragon Breeder]";
	if (Class == Job_Rune_Knight || Class == Job_Rune_Knight_T || Class == Job_Baby_Rune) {
		mes "Welcome. Would you like to rent a Dragon?";
		next;
		switch(select("Yes:No")) {
		case 1:
			//if (!getskilllv(RK_DRAGONTRAINING)) {
			if (!getskilllv(KN_RIDING)) {
				mes "[Dragon Breeder]";
				mes "Please learn how to ride a Dragon first.";
				close;
			} else if (checkmount()) {
				mes "[Dragon Breeder]";
				mes "You already have a Dragon.";
				close;
			} else if(hascashmount()) {
				mes "[Dragon Breeder]";
				mes "Please remove your cash mount.";
				close;
			}
			setmount(MOUNT_DRAGON);
			close;
		case 2:
			mes "[Dragon Breeder]";
			mes "I see. Then have a great day.";
			close;
		}
	}
	mes "What are you doing here?";
	mes "Only Rune Knights can rent a Dragon.";
	close;
}

//== Dragon/Gryphon Master =================================
prontera,126,212,5	script	Riding Creature Master	4_M_ROYALGUARD,{
	mes "[Riding Creature Master]";
	if (Class == Job_Rune_Knight || Class == Job_Rune_Knight_T || Class == Job_Baby_Rune) {
		mes "Welcome. Would you like to rent a Dragon?";
		next;
		switch(select("Yes:No")) {
		case 1:
			//if (!getskilllv(RK_DRAGONTRAINING)) {
			if (!getskilllv(KN_RIDING)) {
				mes "[Riding Creature Master]";
				mes "Please learn how to ride a Dragon first.";
				close;
			} else if (checkmount()) {
				mes "[Riding Creature Master]";
				mes "You already have a Dragon.";
				close;
			} else if(hascashmount()) {
				mes "[Riding Creature Master]";
				mes "Please remove your cash mount.";
				close;
			}
			setmount(MOUNT_DRAGON);
			close;
		case 2:
			mes "[Riding Creature Master]";
			mes "I see. Then have a great day.";
			close;
		}
	}
	if (Class == Job_Royal_Guard || Class == Job_Royal_Guard_T || Class == Job_Baby_Guard) {
		mes "Welcome. Would you like to rent a Gryphon?";
		next;
		switch(select("Yes:No")) {
		case 1:
			if(!getskilllv(KN_RIDING)) {
				mes "[Riding Creature Master]";
				mes "Please learn how to ride a Gryphon first.";
				close;
			} else if (checkmount()) {
				mes "[Riding Creature Master]";
				mes "You already have a Gryphon.";
				close;
			} else if(hascashmount()) {
				mes "[Riding Creature Master]";
				mes "Please remove your cash mount.";
				close;
			}
			setmount(MOUNT_PECO);
			close;
		case 2:
			mes "[Riding Creature Master]";
			mes "I see. Then have a great day.";
			close;
		}
	}
	mes "I'm here to provide Rune Knights and Royal Guards with riding creatures.";
	close;
}

geffen,100,55,3	duplicate(Riding Creature Master)	Riding Creature Master#2	4_M_ROYALGUARD
payon,166,102,5	duplicate(Riding Creature Master)	Riding Creature Master#3	4_M_ROYALGUARD
aldebaran,133,109,5	duplicate(Riding Creature Master)	Riding Creature Master#4	4_M_ROYALGUARD
yuno,171,187,3	duplicate(Riding Creature Master)	Riding Creature Master#5	4_M_ROYALGUARD
rachel,106,130,5	duplicate(Riding Creature Master)	Riding Creature Master#6	4_M_ROYALGUARD

//== Peco removing NPC =====================================
prontera,124,210,5	script	Peco Peco Remover	8W_SOLDIER,{
	mes "[Soldier]";
	mes "If you're unable to dismount from a Peco Peco";
	mes "for some unknown reason,";
	mes "please feel free to use my services.";
	if (checkmount() == MOUNT_PECO) {
		next;
		mes "[Soldier]";
		mes "You're riding a Peco Peco.";
		mes "Would you like to dismount?";
		next;
		switch(select("Yes:No")) {
		case 1:
			setmount(MOUNT_NONE);
			mes "[Soldier]";
			mes "Say, how does it feel to";
			mes "step on the ground on your own";
			mes "feet again?";
			close;
		case 2:
			mes "[Soldier]";
			mes "I see. Please feel free to ask me";
			mes "if you change your mind.";
			close;
		}
	}
	close;
}

//== Magic Gear Renter =====================================
-	script	::mgm	-1,{
	mes "[Magic Gear Master]";
	if (Class == Job_Mechanic || Class == Job_Mechanic_T || Class == Job_Baby_Mechanic) {
		mes "Welcome, Mechanic.";
		mes "Would you like to rent a Pushcart or";
		mes "ride a Magic Gear?";
		next;
		switch(select("Rent a Pushcart:Ride a Magic Gear:Cancel")) {
		case 1:
			if (checkcart()) {
				mes "[Magic Gear Master]";
				mes "I'm sorry, but you already";
				mes "have a Pushcart.";
				close;
			}
			setcart;
			mes "[Magic Gear Master]";
			mes "There you go!";
			close;
		case 2:
			if (checkmount() == MOUNT_MADO) {
				mes "[Magic Gear Master]";
				mes "I'm sorry, but you're already";
				mes "riding a Magic Gear.";
				close;
			} else if (!getskilllv(NC_MADOLICENCE)) {
				mes "[Magic Gear Master]";
				mes "Please learn the skill to get the Magic Gear License first.";
				close;
			} else if(hascashmount()) {
				mes "[Magic Gear Master]";
				mes "Please remove your cash mount.";
				close;
			}
			setmount(MOUNT_MADO);
			mes "[Magic Gear Master]";
			mes "Have fun, and please come again!";
			close;
		case 3:
			close;
		}
	}
	mes "How may I help you?";
	mes "Magic Gears are only available for Mechanics.";
	close;
}
prontera,171,226,3	duplicate(mgm)	Magic Gear Master#prt	4_M_RACHOLD
geffen,103,55,5	duplicate(mgm)	Magic Gear Master#gef	4_M_RACHOLD
payon,166,106,5	duplicate(mgm)	Magic Gear Master#pay	4_M_RACHOLD
aldebaran,133,112,5	duplicate(mgm)	Magic Gear Master#alde	4_M_RACHOLD
yuno,167,187,3	duplicate(mgm)	Magic Gear Master#yuno	4_M_RACHOLD
rachel,106,134,5	duplicate(mgm)	Magic Gear Master#ra	4_M_RACHOLD
dicastes01,187,207,3	duplicate(mgm)	Magic Gear Master#dic	4_M_RACHOLD
manuk,273,212,5	duplicate(mgm)	Magic Gear Master#man	4_M_RACHOLD
splendide,180,174,5	duplicate(mgm)	Magic Gear Master#spl	4_M_RACHOLD
mid_camp,242,243,3	duplicate(mgm)	Magic Gear Master#mid	4_M_RACHOLD
